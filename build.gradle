buildscript {
    repositories {
        maven {
            url "https://hyperiumjailbreak.mycloudrepo.io/public/repositories/maven-central-proxy"
        }
    }
    dependencies {
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.50'
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.1.0'
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

version = "0.1"

configurations {
    embed
    compile.extendsFrom(embed)
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = "com.hyperiumjailbreak.InstallerMain"
archivesBaseName = "LibInstaller"

dependencies {
    compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.50'
    compile 'com.google.code.gson:gson:2.2.4'
    embed 'com.github.bkenn:kfoenix:0.1.3'
    embed 'no.tornado:tornadofx:1.7.19'
    embed 'org.apache.httpcomponents:httpclient:4.5.6'
}

repositories {
    maven {
        url "https://hyperiumjailbreak.mycloudrepo.io/public/repositories/maven-central-proxy"
    }
}

compileKotlin {
    kotlinOptions.jvmTarget= "1.8"
}

processResources {
    inputs.files "src/main/resources"
    outputs.dir "build/classes/main"
    copy {
        from("src/main/resources")
        into("build/classes/main")
    }
}

jar {
    dependsOn configurations.compile
    from {
        configurations.embed.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    from 'LICENSE'
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    manifest.attributes(
        "Manifest-Version": 1.0,
        "Main-Class": "com.hyperiumjailbreak.InstallerMain"
    )
}

shadowJar {
    manifest {
        inheritFrom project.tasks.jar.manifest
    }
    configurations = [project.configurations.compile, project.configurations.embed]
    relocate 'org.apache.http', 'com.hyperiumjailbreak.http'
    minimize()
}
